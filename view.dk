#include "dkli.dkh"
#!

module "view.dkl"
{
	#!
	parse_view_file::file
	{
		if contains(file,"$")
		{
			//Notación de vista en archivo de Websencia
			fn=list.str(split(file,"$"),0)
			vn=list.str(split(file,"$"),1)

			if file.extension(fn)=="" { fn=fn+@websencia_ext }
			if file.exist(path.concat(@entity_path,fn))
			{
				return path.concat(@entity_path,fn)+"$"+vn
			}
			if file.exist(path.concat(@crud_path,fn))
			{
				return path.concat(@crud_path,fn)+"$"+vn
			}

			return ""
		}
		
		if file.extension(file)=="" { file=file+@dkl_view_ext }
		if file.exist(path.concat(@entity_path,file))
		{
			return path.concat(@entity_path,file)
		}
		if file.exist(path.concat(@crud_path,file))
		{
			return path.concat(@crud_path,file)
		}

		return ""
	}
	
	#!
	view_controller::operation, &params
	{
		params<"_operation">:operation

		if @expected_html
		{
			point @fail_view to error_view

			switch operation
			{
				case "list" { point @success_view to list_view }
				case "read" { point @success_view to form_view }
				case "create" 
				{ 
					point @success_view to redirect 
					point @fail_view to form_view
				}
				case "update" 
				{ 
					point @success_view to redirect 
					point @fail_view to form_view
				}
				case "delete" { point @success_view to redirect }
			}
		}
	}

	#!
	redirect :: &params, &input_data, &output_data, &error_info
	{
		rel_path=ifstr(@@(params,"_entity_id")!="","../","./")
		@http_context<"response/headers/Location">:rel_path+"?_select="+@@(params,"_entity_id")
	}

	render_view::file,&params, &input_data, &output_data, &error_info
	{
		html=""
		ref context=@null

		if not(isnull(error_info)) 
		{
			ref context=input_data
			if isnull(context){ ref context=record.create() }
			context<"_error*">:error_info
		}
		else
		{
			ref context=output_data
			using context
			{
				@"_parameters*":params
				@"_input*":input_data
			}
		}

		if contains(file,"$")
		{
			fn=list.str(split(file,"$"),0)
			vn=list.str(split(file,"$"),1)
			context<"_websencia_file">:fn
			context<"_websencia_view">:vn

			html=runscript(path.concat(@crud_path,"websencia.dk"),context,"crud_context")
		}
		else
		{
			ext=tolower(file.extension(file))
			ext=ifstr(ext==".dkl",".dk",ext)

			switch ext
			{
				case ".dk"
				{
					html=runscript(file,context,"crud_context")
				}
				default
				{
					do @context.begin(context)
					
					html=ftext(file.rtext(file))

					do @context.end()
				}
			}
		}

		@http_context<"response/headers/content-type">:"text/html;charset=utf-8"
    	@http_context<"response/output">:"text"
		@http_context<"response/text">: html
	}

	#!
	error_view:: &params, &input_data, &output_data, &error_info
	{
		do render_view(@error_view,params, input_data, output_data,error_info)
	}

	#!
	list_view:: &params, &input_data, &output_data, &error_info
	{
		do render_view(@list_view,params, input_data, output_data,error_info)
	}

	#!
	flush_stdout::
	{
		@http_context<"response/headers/content-type">:"text/html;charset=utf-8"
    	@http_context<"response/output">:"text"

		@http_context<"response/text">:  fxml()
	}

	#!
	form_view:: &params, &input_data, &output_data, &error_info
	{
		do render_view(@form_view,params, input_data, output_data,error_info)
	}

	#!
	json_view:: &params, &input_data, &output_data, &error_info
	{
		@http_context<"response/headers/content-type">:"application/json;charset=utf-8"
    	@http_context<"response/output">:"text"

		if isnull(error_info)
		{
			@http_context<"response/text">:to.json(output_data)
		}
		else
		{
			@http_context<"response/text">:to.json(error_info)
		}
	}

	#!
	set_http_status:: status
	{
		@http_context<"response/headers/status">:status
	}

	#!
	ref @success_view=@null
	ref @fail_view=@null
	ref @view_controller=@null

	point @success_view to json_view
	point @fail_view to json_view
	point @view_controller to view_controller

}